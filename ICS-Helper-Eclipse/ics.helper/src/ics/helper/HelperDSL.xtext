grammar ics.helper.HelperDSL with org.eclipse.xtext.common.Terminals

generate helperDSL "http://www.helper.ics/HelperDSL"

Model:
    commands+=Command*;

Command:
    CreateCommand | SplitCommand | MergeCommand | ModifyCommand;

CreateCommand:
    'create' 'schedule' name=ID '{'
        events+=Event*
    '}';

  
Event:
    'event' name=ID 
    'start' startTime=STRING 
    'end' endTime=STRING 
    ('location' location=STRING)?
    ('description' description=STRING)?
    ('recur' recur=RecurRule (daysOfWeek=DaysOfWeek)?)?
    ('link' link=STRING)?
    ('organizer' organizer=Person)?
    ('invitees' '{' invitees+=Person+ '}')?
    ('reminder' reminder=Reminder)?;

Reminder:
	'time' time=INT
	('title' title=STRING)?
;

Person:
    'name' name=STRING 
    'email' email=STRING
;

enum RecurRule:
    DAILY = 'daily' |
    WEEKLY = 'weekly' |
    MONTHLY = 'monthly' |
    YEARLY = 'yearly'|
    BYDAY = 'weekly on';

DaysOfWeek: days+=WEEKDAY (',' days+=WEEKDAY)*; // Example: "on Monday,Wednesday,Friday"

enum WEEKDAY:
	MO = 'Monday' |
	TU = 'Tuesday' |
	WE = 'Wednesday' |
	TH = 'Thursday' |
	FR = 'Friday' |
	SA = 'Saturday' |
	SU = 'Sunday'
;
 
Schedule:
	name=ID;

SplitCommand:
    'split' schedule=[Schedule] 'by' criteria=STRING;

MergeCommand:
    'merge' schedules+=[Schedule]+ 'into' target=ID;

ModifyCommand:
    'modify' ('schedule' schedule=[Schedule] | 'event' event=[Event]) 'set' (time='time' value=STRING | 'owner' value=STRING | 'location' value=STRING);
